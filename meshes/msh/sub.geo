//-----------------------------------------------------------------------------
//
// Name       : sub.geo
// Author     : Mohd Afeef BADRI
// Date       : 26 / Jul / 2024
//
// ----------------------------------------------------------------------------
// Comment    : simple submarine problem mesh for ill posed acoustics
//
//
// Usage      : gmsh sub.geo -2 -format msh41 -clmin 1 -clmax 1
//              gmsh sub.geo -2 -format msh41 -setnumber MeshSize 1.0 -setnumber Rfactor 2.0 
//
//-----------------------------------------------------------------------------


//==============================================================================
// ---- define parameters for commandline ----
//==============================================================================

DefineConstant[ meshSize  = {1.0, Min 1e-8, Max 100, Step 1,
                         Name "Parameters/MeshSize MeshSize"} ];

DefineConstant[ Rfactor  = {1.0, Min 1, Max 100, Step 1,
                         Name "Parameters/Rfactor Rfactor"} ];

//==============================================================================
// ---- outer water circle ----
//==============================================================================
SetFactory("OpenCASCADE");
Circle(1) = {-0, -0, 0, 10, 0, 2*Pi};

//==============================================================================
// ---- submarine geometry ----
//==============================================================================
meshSize /= Rfactor;

Point(2) = {-2.7, 0, 0, meshSize};
Point(3) = {-2.3, 0.4, 0, meshSize};
Point(4) = {-1.1, 0.4, 0, meshSize};
Point(5) = {-1, 0.7, 0, meshSize};
Point(6) = {-0.8, 0.7, 0, meshSize};
Point(7) = {-0.7, 0.4, 0, meshSize};
Point(8) = {0.3, 0.3, 0, meshSize};
Point(9) = {0.6, 0.2, 0, meshSize};
Point(10) = {0.7, 0.4, 0, meshSize};
Point(11) = {0.9, 0.4, 0, meshSize};
Point(12) = {0.9, -0, 0, meshSize};
Point(13) = {-6.2, -2.3, 0, meshSize};

Spline(2) = {2, 3, 4};
Line(3) = {4, 5};
Line(4) = {5, 6};
Line(5) = {6, 7};
Line(6) = {7, 8};
Line(7) = {8, 9};
Line(8) = {9, 10};
Line(9) = {10, 11};
Line(10) = {11, 12};
Point(14) = {-0.9, -0.2, 0, meshSize};
Spline(11) = {2, 14, 12};

//==============================================================================
// ---- water volume ----
//==============================================================================
Curve Loop(1) = {1};
Curve Loop(2) = {2, 3, 4, 5, 6, 7, 8, 9, 10, -11};
Plane Surface(1) = {1, 2};

//==============================================================================
// ---- physical tags ----
//==============================================================================
Physical Curve("outer", 12) = {1};
Physical Curve("inner1", 13) = {2, 11};
Physical Curve("inner2", 14) = {3, 4, 5, 6, 7, 8, 9, 10};
Physical Surface("surface", 15) = {1};

//==============================================================================
// ---- msh version imposed ----
//==============================================================================
Mesh.MshFileVersion = 4.1;
